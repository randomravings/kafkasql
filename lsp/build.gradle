plugins {
  id 'java'
  id 'application'
  id 'com.gradleup.shadow' version '9.0.0'
}

dependencies {
  implementation project(':runtime')
  implementation project(':lang')
  implementation project(':linter')
  implementation project(':pipeline')
  implementation 'org.eclipse.lsp4j:org.eclipse.lsp4j:0.19.0'
}

application {
  mainClass = 'kafkasql.lsp.Main'
}

tasks.register('runLanguageServer', JavaExec) {
  group = 'application'
  description = 'Run the KafkaSQL language server (stdio)'
  classpath = sourceSets.main.runtimeClasspath
  mainClass.set(application.mainClass)
  standardInput = System.in
}

shadowJar {
  archiveClassifier.set('all')
  manifest {
    attributes 'Main-Class': application.mainClass.get()
  }
}

tasks.named('build') {
  dependsOn tasks.named('shadowJar')
}